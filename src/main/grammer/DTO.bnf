{
  parserClass="net.fallingangel.jimmerdto.parser.DTOParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  implements="net.fallingangel.jimmerdto.psi.mixin.DTOElement"

  psiClassPrefix="DTO"
  psiImplClassSuffix="Impl"
  psiPackage="net.fallingangel.jimmerdto.psi"
  psiImplPackage="net.fallingangel.jimmerdto.psi.impl"

  elementTypeHolderClass="net.fallingangel.jimmerdto.psi.DTOTypes"
  elementTypeClass="net.fallingangel.jimmerdto.psi.DTOElementType"
  tokenTypeClass="net.fallingangel.jimmerdto.psi.DTOTokenType"

  psiImplUtilClass="net.fallingangel.jimmerdto.util.DTOPsiUtil"

  tokens=[
    COMMA=","
    DOT="."
    AT="@"
    EQ="="
    NE="<>"
    LT="<"
    LE="<="
    GT=">"
    GE=">="
    COLON=":"
    SEMICOLON=";"
    HASH="#"
    PLUS="+"
    MINUS="-"
    OPTIONAL="?"
    REQUIRED="!"
    ASTERISK="*"
    POWER="^"
    DOLLAR="$"
    ARROW="->"
    SLASH="/"
    CLASS_REFERENCE="::"
    PAREN_L="("
    PAREN_R=")"
    BRACKET_L="["
    BRACKET_R="]"
    BRACE_L="{"
    BRACE_R="}"

    IDENTIFIER="regexp:[$A-Za-z_][$\\w]*"
    WHERE_KEYWORD="!where"
    ORDER_BY_KEYWORD="!orderBy"
    FILTER_KEYWORD="!filter"
    RECURSION_KEYWORD="!recursion"
    FETCH_TYPE_KEYWORD="!fetchType"
    LIMIT_KEYWORD="!limit"
    OFFSET_KEYWORD="!offset"
    BATCH_KEYWORD="!batch"
    DEPTH_KEYWORD="!depth"

    BOOLEAN_CONSTANT="regexp:true|false"
    INTEGER_CONSTANT="regexp:\\d+"
    FLOAT_CONSTANT="regexp:\\d+\\.\\d+"
    STRING_CONSTANT="regexp:\"[^\"]*\""
    SQL_STRING_CONSTANT="regexp:'[^']*'"
    CHAR_CONSTANT="regexp:'[^']'"
  ]
}

dto-file ::= export-statement? import-statement* dto*

export-statement ::= export qualified-type ("->" package-statement)?

package-statement ::= package qualified-type

import-statement ::= import qualified-type grouped-types?

grouped-types ::= "." "{" qualified-type ("," qualified-type)* "}"

dto ::= annotation* modifier* dto-name interfaces? dto-body {
    pin = 3
}

dto-name ::= IDENTIFIER

dto-body ::=
"{"
    ((user-prop | macro | positive-prop | alias-group | negative-prop) ("," | ";")?)*
"}"

macro ::= "#" macro-name macro-args? ("?" | "!")? {
    pin = 1
}

macro-name ::= IDENTIFIER

macro-args ::= "(" (macro-arg ("," macro-arg)*)? ")"

macro-arg ::= IDENTIFIER {
    implements=["net.fallingangel.jimmerdto.psi.mixin.DTOElement"; "net.fallingangel.jimmerdto.psi.mixin.DTONamedElement"]
    mixin="net.fallingangel.jimmerdto.psi.mixin.impl.DTONamedElementImpl"
    methods=[getName setName invoke unaryPlus]
}

alias-group ::= as "(" alias-pattern? ")" alias-group-body

alias-group-body ::= "{" (macro | positive-prop)* "}"

alias-pattern ::= original "->" replacement? {
    pin = 2
}

original ::= alias-power? IDENTIFIER? alias-dollar?

alias-power ::= POWER

alias-dollar ::= DOLLAR

replacement ::= STRING_CONSTANT | IDENTIFIER

positive-prop ::= (annotation | prop-config)* "+"? modifier? prop-name prop-flags? prop-args? ("?" | "!" | "*")? (as prop-alias)? interfaces? (enum-body | prop-body)? {
    pin = 4
}

prop-name ::= IDENTIFIER | null {
    implements=["net.fallingangel.jimmerdto.psi.mixin.DTOElement"; "net.fallingangel.jimmerdto.psi.mixin.DTONamedElement"]
    mixin="net.fallingangel.jimmerdto.psi.mixin.impl.DTONamedElementImpl"
    methods=[getName setName invoke unaryPlus]
}

prop-flags ::= "/" "i$"? "i"? POWER? DOLLAR?

//prop-args ::= "(" (named-parameter | value) ("," (named-parameter | value))* ")"
prop-args ::= "(" (value ("," value)*)? ")"

prop-alias ::= IDENTIFIER

prop-body ::= annotation* dto-body {
    pin = 2
}

enum-body ::= "->" "{" (enum-instance-mapping ("," | ";")?)* "}"

enum-instance-mapping ::= enum-instance ":" enum-instance-value {
    pin = 1
}

enum-instance ::= IDENTIFIER {
    implements=["net.fallingangel.jimmerdto.psi.mixin.DTOElement"; "net.fallingangel.jimmerdto.psi.mixin.DTONamedElement"]
    mixin="net.fallingangel.jimmerdto.psi.mixin.impl.DTONamedElementImpl"
    methods=[getName setName invoke unaryPlus]
}

enum-instance-value ::= (INTEGER_CONSTANT | STRING_CONSTANT)

negative-prop ::= "-" prop-name {
    pin = 2
}

user-prop ::= annotation* prop-name ":" type-def {
    pin = 3
}

generic-args ::= "<" generic-arg ("," generic-arg)* ">" {
    pin = 1
}

generic-arg ::= "*" | modifier? type-def

/// GLOBAL ///
modifier ::= "input" | "specification" | "abstract" | "unsafe" | "dynamic" | "fixed" | "static" | "fuzzy"| "out" | "in"

qualified-name ::= qualified-name-part ("." qualified-name-part)*

qualified-name-part ::= IDENTIFIER {
    implements=["net.fallingangel.jimmerdto.psi.mixin.DTOElement"; "net.fallingangel.jimmerdto.psi.mixin.DTONamedElement"]
    mixin="net.fallingangel.jimmerdto.psi.mixin.impl.DTONamedElementImpl"
    methods=[getName setName invoke unaryPlus]
}

qualified-type ::= qualified-name (as qualified-type-alias)?

qualified-type-alias ::= IDENTIFIER

type-def ::= qualified-name generic-args? "?"?

interfaces ::= implements type-def ("," type-def)* {
    pin = 1
}

// parameter
//named-parameter ::= parameter-name "=" value

//parameter-name ::= IDENTIFIER

value ::= CHAR_CONSTANT | STRING_CONSTANT |
             INTEGER_CONSTANT | FLOAT_CONSTANT |
             BOOLEAN_CONSTANT | null |
             class-value | enum-value | IDENTIFIER | array-value {
    implements=["net.fallingangel.jimmerdto.psi.mixin.DTOElement"; "net.fallingangel.jimmerdto.psi.mixin.DTONamedElement"]
    mixin="net.fallingangel.jimmerdto.psi.mixin.impl.DTONamedElementImpl"
    methods=[getName setName invoke unaryPlus]
}

enum-value ::= IDENTIFIER ("." IDENTIFIER)+

class-value ::= qualified-name "?"? ("." | "::") class {
    pin = 3
}

array-value ::= "[" (value ("," value)*)? "]" | "{" (value ("," value)*)? "}"

// annotation
annotation ::= annotation-constructor ("(" (annotation-value | annotation-parameter) ("," annotation-parameter)* ")")? {
    pin = 1
}

annotation-constructor ::= "@" annotation-name {
    pin = 1
}

annotation-name ::= IDENTIFIER ("." IDENTIFIER)* {
    pin = 1
}

nest-annotation ::= "@"? annotation-name "(" ((annotation-value | annotation-parameter) ("," annotation-parameter)*)? ")" {
    pin = 3
}

// annotation parameter
annotation-parameter ::= IDENTIFIER "=" annotation-value {
    pin = 1
}

annotation-value ::= CHAR_CONSTANT | STRING_CONSTANT |
             INTEGER_CONSTANT | FLOAT_CONSTANT |
             BOOLEAN_CONSTANT | null |
             nest-annotation | annotation | class-value | enum-value | annotation-array-value

annotation-array-value ::= "[" (annotation-value ("," annotation-value)*)? "]" | "{" (annotation-value ("," annotation-value)*)? "}"

// prop-config
prop-config ::= where | order-by | filter | recursion | fetchType | limit | offset | batch | depth

where ::= "!where" where-args

where-args ::= "(" (predicate ((and | or) predicate)*)? ")"

predicate ::= compare | nullity

compare ::= qualified-name compare-symbol compare-value

compare-symbol ::= "=" | "<>" | "<" | "<=" | ">" | ">="

compare-value ::= BOOLEAN_CONSTANT | CHAR_CONSTANT | FLOAT_CONSTANT | INTEGER_CONSTANT | SQL_STRING_CONSTANT

nullity ::= qualified-name is not? null

order-by ::= "!orderBy" order-by-args

order-by-args ::= "(" (order-by-arg ("," order-by-arg)*)? ")"

order-by-arg ::= qualified-name (asc | desc)?

filter ::= "!filter" "(" qualified-name ")"

recursion ::= "!recursion" "(" qualified-name ")"

fetchType ::= "!fetchType" "(" IDENTIFIER ")"

limit ::= "!limit" "(" INTEGER_CONSTANT ")"

offset ::= "!offset" "(" INTEGER_CONSTANT ")"

batch ::= "!batch" "(" INTEGER_CONSTANT ")"

depth ::= "!depth" "(" INTEGER_CONSTANT ")"